import com.android.build.api.dsl.CommonExtension

plugins {
    id 'com.android.application' version '8.1.2' apply false
    id 'com.android.library' version '8.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.0' apply false
}


ext {
    minSdkVersion = 23
    compileSdkVersion = 34
    targetSdkVersion = 34
}


def setupDefaultModuleConfig(CommonExtension it) {
    it.compileSdkVersion rootProject.ext.compileSdkVersion

    it.defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    it.buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    it.compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    it.kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs = ['-Xjvm-default=all']
    }

    it.testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.returnDefaultValues = true
        unitTests {
            includeAndroidResources = true
        }
    }
}
